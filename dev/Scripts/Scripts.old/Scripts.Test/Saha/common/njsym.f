*
*     ------------------------------------------------------------------
*	G E N I 9
*     ------------------------------------------------------------------
*
      SUBROUTINE GENI9(IP)
*
*      DETERMINES THE NUMBER OF RECOUPLING NECESSARY TO BRING J2(I5,I3)
*      AND J2(I6,I4) INTO THE SAME TRIAD. THIS WILL GIVE A TRIAD
*      IDENTICAL WITH ONE IN J3. ON EXIT I9 CONTAINS THE NUMBER OF
*      RECOUPLINGS PLUS TWO,I7 CONTAINS THE LEVEL OF THE I5 TRIAD BELOW
*      THE COMMON TRIAD AND I8 CONTAINS THE LEVEL OF THE I6 TRIAD BELOW
*      THE COMMON TRIADS
*      SEE DESCRIPTION OF COMMON BLOCK WCOMI9 FOR FURTHER DETAILS
*
      COMMON/COUPLE/M,N,J1(60),J2(18,3),J3(18,3)
      COMMON/DEPTHS/J4(60),J5(60)
      COMMON/WCOMI9/I3,I4,I5,I6,I7,I8,I9,I17,I18,I19,I20
*
*
*
      I1 = J4(I7)
      I2 = J4(I8)
*
*      DETERMINES WHICH J OF J2(I5,I3) AND J2(I6,I4) LIES LOWEST, STORE
*      LOWEST AS J2(I18,I20) AND HIGHEST AS J2(I17,I19)
*
      IF (I1-I2) 1,1,3
 1    I17 = I5
      I18 = I6
      I19 = I3
      I20 = I4
      I3 = I2-I1
      I7 = 0
      I8 = I3
      I4 = I1
      IF (I3) 8,8,2
*
*      I6 DENOTES THE LOWEST TRIAD,SCAN TRIADS TO FIND NEW TRIAD I6 AT
*      SAME LEVEL AS I5
*
 2    DO 4 I = 1,I3
      DO 5 J = 1,IP
      IF (J2(J,1)-J2(I6,3)) 7,6,7
 7    IF (J2(J,2)-J2(I6,3)) 5,6,5
 5    CONTINUE
      J=IP
 6    I6 = J
 4    CONTINUE
      GO TO 8
 3    I17 = I6
      I18 = I5
      I19 = I4
      I20 = I3
      I3 = I1-I2
      I7 = I3
      I8 = 0
*
*      I5 DENOTES THE LOWEST TRIADS. SCAN TRIADS TO FIND NEW TRIAD I6 AT
*      SAME LEVEL I5
*
      DO 9 I = 1,I3
      DO 12 J = 1,IP
      IF (J2(J,1)-J2(I5,3)) 10,11,10
 10   IF (J2(J,2)-J2(I5,3)) 12,11,12
 12   CONTINUE
      J=IP
 11   I5 = J
 9    CONTINUE
      I4 = I2
*
*      I5 AND I6 NOW DENOTES TRIADS AT SAME LEVEL. I4 CONTAINS THE
*      COMMON LEVEL
*
 8    DO 13 I = 1,I4
      I1 = I
      IF (I5-I6) 14,21,14
*
*      I5 AND I6 DENOTE DIFFERENT TRIADS SCAN TO FIND TRIADS AT NEXT
*      LEVEL WHICH REPLACE I5 AND I6
*
 14   DO 15 J = 1,IP
      IF (J2(J,1)-J2(I5,3)) 16,17,16
 16   IF (J2(J,2)-J2(I5,3)) 15,17,15
 15   CONTINUE
      J=IP
17    I5 = J
      DO 18 J = 1,IP
      IF (J2(J,1)-J2(I6,3)) 19,20,19
 19   IF (J2(J,2)-J2(I6,3)) 18,20,18
 18   CONTINUE
      J=IP
 20   I6 = J
 13   CONTINUE
*
*      I5 AND I6 NOW BOTH DENOTE THE COMMON TRIAD
*
 21   I9 = I3+2*I1
      I8 = I8+I1
      I7 = I7+I1
      RETURN
      END
*
*     ------------------------------------------------------------------
*	G E N J 4 5
*     ------------------------------------------------------------------
*
      SUBROUTINE GENJ45(IP)
*
*      FIND THE LEVEL OF EACH J IN THE COUPLING TREES OF J2 AND J3 AND
*      STORE IN THE J4 AND J5 ARRAYS RESPECTIVELY. IF AN ELEMENT OF J1
*      DOES NOT OCCUR IN J2 THE J4 ENTRY IS -1 AND IF AN ELEMENT DOES
*      NOT OCCUR IN J3 THE J5 ENTRY IS -1
*
      COMMON/COUPLE/M,N,J1(60),J2(18,3),J3(18,3)
      COMMON/DEPTHS/J4(60),J5(60)
*
*
*
      DO 1 I=1,M
      DO 2 I2=1,IP
*
*      STORE LEVEL OF EACH J IN J2 ARRAY IN J4
*
      IF (J2(I2,1)-I) 3,4,3
 3    IF (J2(I2,2)-I) 2,4,2
 2    CONTINUE
      DO 17 I2 = 1,IP
      IF (J2(I2,3)-I) 17,18,17
 17   CONTINUE
      J4(I) = -1
      GO TO 5
 18   J4(I) = 0
      GO TO 5
 4    I3 = 1
 9    DO 6 I4 = 1,IP
      IF (J2(I4,1)-J2(I2,3)) 7,8,7
 7    IF (J2(I4,2)-J2(I2,3)) 6,8,6
 6    CONTINUE
      J4(I) = I3
      GO TO 5
 8    I3 = I3+1
      I2 = I4
      GO TO 9
*
*      STORE LEVEL OF EACH J IN J3 ARRAY IN J5
*
 5    DO 10 I2 = 1,IP
      IF (J3(I2,1)-I) 11,12,11
 11   IF (J3(I2,2)-I) 10,12,10
 10   CONTINUE
      DO 19 I2 = 1,IP
      IF (J3(I2,3)-I) 19,20,19
 19   CONTINUE
      J5(I) = -1
      GO TO 1
 20   J5(I) = 0
      GO TO 1

 12   I3 = 1
 16   DO 13 I4 = 1,IP
      IF (J3(I4,1)-J3(I2,3)) 14,15,14
14    IF (J3(I4,2)-J3(I2,3)) 13,15,13
 13   CONTINUE
      J5(I) = I3
      GO TO 1
 15   I3 = I3+1
      I2 = I4
      GO TO 16
 1    CONTINUE
      RETURN
      END
*
*     ------------------------------------------------------------------
*	G E N S U M
*     ------------------------------------------------------------------
*
      SUBROUTINE GENSUM(J6C,J7C,J8C,JWC,J6,J7,J8,JW,RECUP)
*
      IMPLICIT REAL *8(A-H,O-Z)
*
*
*      CARRIES OUT THE SUMMATION OVER COEFFICIENTS DEFINED BY THE ARRAYS
*      J6,J7,J8 AND JW TO GIVE RECUP
*      THE ENTRY IS EITHER MADE FROM NJSYM OR DIRECTLY ASSUMING THAT THE
*      ARRAYS J6,J7,J8 AND JW HAVE ALREADY BEEN DETERMINED BY A PREVIOUS
*      ENTRY TO NJSYM AND THAT THE SUMMATION IS REQUIRED FOR ANOTHER SET
*      OF J VALUES DEFINED BY THE ARRAY J1
*      THE DEFINITION OF THE ARGUMENT LIST IS GIVEN AT BEGINNING OF
*      NJSYM
*
      DIMENSION IST(6),JWORD(6,20),J6P(60),J7P(80),J8P(60),JSUM1(18),
     :JSUM2(18),JSUM4(18,20),JSUM5(18,20),JSUM3(18),JSUM6(18),
     :JSUM7(18),JSUM8(18),JSUM(2,20),JWTEST(20),WSTOR(20),IPAIR(2,2)
      DIMENSION J6(60),J7(80),J8(60),JW(6,20)
      COMMON/COUPLE/M,N,J1(60),J2(18,3),J3(18,3)
      COMMON/DEBUG/IBUG1,IBUG2,IBUG3,NBUG6,NBUG7,IFULL
      COMMON/DIMEN/KFL1,KFL2,KFL3,KFL4,KFL5,KFL6,KFL7,MXIHSH
      COMMON/INFORM/IREAD,IWRITE,IOUT,ISC1,ISC2,ISC3,JSC0,JSC1,JSC2,JSC3
*
*      FORMAT STATEMENTS USED IN GENSUM
*
   35 FORMAT (21H FAIL IN GENSUM AT 35)
   36 FORMAT (21H FAIL IN GENSUM AT 36)
   42 FORMAT (21H FAIL IN GENSUM AT 42)
   63 FORMAT (21H FAIL IN GENSUM AT 63)
  169 FORMAT(22H 169...        RECUP =,F12.8,7H STOR =,F12.8,8H STOR1 =,
     :F12.8)
  170 FORMAT(18H 170...       IST ,6I4)
  190 FORMAT(8H WSTOR =,10F10.6)
  308 FORMAT(23H KFL6 DIMENSION FAILURE)
  311 FORMAT(22H FAIL IN GENSUM AT 310)
*
*
*
*
*
*      S E C T I O N  1
*
*      EVALUATES ALL TERMS IN J6,J7,J8 AND JW WHICH DO NOT INVOLVE A
*      SUMMATION AND FORM MODIFIED ARRAYS J6P,J7P,J8P AND JWORD WHICH DO
*      THE RESULT OF THE EVALUATION IS STORED IN RECUP AND AISTOR
*
      RECUP=1.D0
      MAXJWE=M
      JWRD = 0
      IF(JWC)302,302,185
*
*      MULTIPLY RECUP BY ALL RACAH COEFFICIENTS WHICH DO NOT INVOLVE A
*      SUMMATION
*
  185 DO 1 I=1,JWC
      DO 2 J=1,6
      IF(JW(J,I)-M) 2,2,3
    2 CONTINUE
      DO 4 J=1,6
      I1=JW(J,I)
      IST(J) = J1(I1) - 1
    4 CONTINUE
      CALL GRACAH(IST(1),IST(2),IST(3),IST(4),IST(5),IST(6),X1)
      RECUP = RECUP*X1
      GO TO 1
*
*      JWRD IS THE NUMBER OF RACAH COEFFICIENTS WHICH INVOLVE A
*      SUMMATION
*      JWORD(I,J),I=1,6,J=1,JWRD CONTAINS THE NUMBER WHICH GIVE THE
*      LOCATION OF THE J VALUES FOR THE RACAH COEFFICIENTS EITHER IN THE
*      J1 LIST OR IN THE JSUM1 LIST
*
    3 JWRD = JWRD+1
      DO 5 J=1,6
      JWORD(J,JWRD)=JW(J,I)
*
*      MAXJWE CONTAINS THE MAXIMUM J IN THE LIST OF VARIABLES TO BE
*      SUMMED OVER
*
      IF(MAXJWE-JW(J,I))  215,5,5
  215 MAXJWE=JW(J,I)
    5 CONTINUE
    1 CONTINUE
  302 J6CP=0
      IF(J6C)300,300,301
*
*      J6P(I),I=1,J6CP CONTAINS ALL J6 WHICH INVOLVE  A SUMMATION
*      MULTIPLY RECUP BY ALL THOSE WHICH DO NOT
*
  301 DO 6 I=1,J6C
      IF(J6(I)-M) 7,7,21
    7 I1=J6(I)
      RECUP = RECUP*DSQRT(DFLOAT(J1(I1)))
      GO TO 6
   21 J6CP = J6CP+1
      J6P(J6CP)=J6(I)
    6 CONTINUE
  300 IASTOR = 0
      J7CP = 0
      IF(J7C) 303,303,304
*
*      J7P(I),I=1,J7CP CONTAINS ALL J7 WHICH INVOLVE  A SUMMATION.
*      MULTIPLY RECUP BY ALL THOSE WHICH DO NOT
*
  304 DO 8 I=1,J7C
      IF(J7(I)-M) 9,9,22
    9 I1=J7(I)
      IASTOR = IASTOR + J1(I1) -1
      GO TO 8
   22 J7CP = J7CP+1
      J7P(J7CP)=J7(I)
    8 CONTINUE
  303 J8CP=0
      IF(J8C) 305,305,306
*
*      J8CP(I),I=1,J8CP CONTAINS ALL J8 WHICH INVOLVE A SUMMATION
*      MULTIPLY RECUP BY ALL THOSE WHICH DO NOT
*
  306 DO 10 I=1,J8C
      IF(J8(I)-M) 11,11,23
   11 I1=J8(I)
      IASTOR = IASTOR - J1(I1) + 1
      GO TO 10
   23 J8CP=J8CP+1
      J8P(J8CP)=J8(I)
   10 CONTINUE
*
*      NO RACAH COEFFICIENTS REMAINING AND THUS NO SUMMATIONS TO BE
*      CARRIED OUT IF JWRD=0. JUMP TO END TO INCLUDE (-1) FACTORS IN
*      RECUP AND THEN EXIT
*
  305 IF(JWRD) 12,12,13
*
*
*
*
*
*      S E C T I O N  2
*
*      SEARCH THROUGH THE JWORD LIST TO FIND ALL THE SUMMATION VARIABLES
*      NSUM IS THE NUMBER OF SUMMATION VARIABLES
*      JSUM1(I),I=1,NSUM CONTAINS A LIST OF ALL SUMMATION VARIABLES IN
*      THE SAME NOTATION AS IN JW LIST
*
   13 NSUM=0
      MAXSUM=MAXJWE-M
      DO 24 I=1,MAXSUM
      JSUM6(I)=0
      JSUM7(I)=0
   24 CONTINUE
*
*      FIND SUMMATION VARIABLES
*
      DO 14 I=1,JWRD
      DO 15 J=1,6
      IF(JWORD(J,I)-M) 15,15,16
   16 NSUM=NSUM+1
      IF(NSUM-1) 17,17,18
*
*      HAS THE SUMMATION VARIABLE OCCURED BEFORE. IF NOT INCLUDE IN
*      JSUM1 LIST
*
   18 NSUM1 = NSUM-1
      DO 19 I1=1,NSUM1
      IF(JWORD(J,I)-JSUM1(I1)) 19,20,19
   19 CONTINUE
   17 JSUM1(NSUM)=JWORD(J,I)
      I1=NSUM
      GO TO 25
   20 NSUM =NSUM1
*
*      JSUM6(I),I=1,NSUM IS THE NUMBER OF TIMES EACH SUMMATION VARIABLE
*      OCCURS IN JWORD
*
   25 JSUM6(I1)=JSUM6(I1)+1
      I2=JSUM6(I1)
*
*      JSUM4(I,J),JSUM5(I,J),I=1,NSUM,J=1,JSUM6(I) IS THE POSITION IN
*      THE JWORD LIST WHERE THE JSUM1 ELEMENT OCCURS
*
      JSUM4(I1,I2)=J
      JSUM5(I1,I2)=I
*
*      (JWORD-M) GIVES LOCATION IN JSUM1 LIST IF A SUMMATION VARIABLE
*
      JWORD(J,I)=M+I1
   15 CONTINUE
   14 CONTINUE
      IF(KFL6.LT.NSUM) THEN
         WRITE(IWRITE,308)
         STOP
      END IF
  307 IF(J6CP) 26,26,27
*
*      CHECK THAT NO EXTRA SUMMATION VARIABLES OCCUR IN J6P. SET J6P
*      EQUAL TO THE LOCATION IN JSUM1 LIST OF SUMMATION VARIABLE
*
   27 DO 28 I=1,J6CP
      DO 29 J=1,NSUM
      IF(J6P(I)-JSUM1(J)) 29,30,29
   29 CONTINUE
      WRITE(IWRITE,35)
      STOP
   30 J6P(I)=J
   28 CONTINUE
   26 IF(J7CP) 130,130,31
*
*      CHECK THAT NO EXTRA SUMMATION VARIABLES OCCUR IN J7P, SET J7P
*      EQUAL TO THE LOCATION IN JSUM1 LIST OF SUMMATION VARIABLE
*
   31 DO 32 I=1,J7CP
      DO 33 J=1,NSUM
      IF(J7P(I)-JSUM1(J)) 33,34,33
   33 CONTINUE
      WRITE(IWRITE,36)
      STOP
   34 J7P(I)=J
   32 CONTINUE
  130 IF(J8CP) 37,37,38
*
*      CHECK THAT NO EXTRA SUMMATION VARIABLES OCCUR IN J8P. SET J8P
*      EQUAL TO THE LOCATION IN JSUM1 LIST OF SUMMATION VARIABLE
*
   38 DO 39 I=1,J8CP
      DO 40 J=1,NSUM
      IF(J8P(I)-JSUM1(J)) 40,41,40
   40 CONTINUE
      WRITE(IWRITE,42)
      STOP
   41 J8P(I)=J
   39 CONTINUE
*
*
*
*
*
*      S E C T I O N  3
*
*      ORDERS THE SUMMATION VARIABLES SO THAT THE RANGE OF EACH
*      SUMMATION HAS BEEN PREVIOUSLY DEFINED
*
   37 NCT  =0
      NCT1 = 0
   64 DO 43 I=1,JWRD
      DO 44 J=1,6
      I1=JWORD(J,I)-M
      IF(I1) 44,44,45
*
*      JSUM7(I),I=1,NSUM IS THE ORDER OF THE SUMMATIONS OVER THE J
*      VARIABLES. INITIALLY THIS ARRAY IS ZERO
*
   45 IF(JSUM7(I1)) 46,46,44
   46 GO TO (47,48,49,50,51,52),J
*
*      THE ROWS OF THE IPAIR ARRAYS GIVE LIMITS OF SUMMATION IMPOSED
*      BY THE TRIANGULAR CONDITION
*
   47 IPAIR(1,1) = JWORD(2,I)
      IPAIR(1,2) = JWORD(5,I)
      IPAIR(2,1) = JWORD(3,I)
      IPAIR(2,2) = JWORD(6,I)
      GO TO 53
   48 IPAIR(1,1) = JWORD(1,I)
      IPAIR(1,2) = JWORD(5,I)
      IPAIR(2,1) = JWORD(4,I)
      IPAIR(2,2) = JWORD(6,I)
      GO TO 53
   49 IPAIR(1,1) = JWORD(1,I)
      IPAIR(1,2) = JWORD(6,I)
      IPAIR(2,1) = JWORD(4,I)
      IPAIR(2,2) = JWORD(5,I)
      GO TO 53
   50 IPAIR(1,1) = JWORD(2,I)
      IPAIR(1,2) = JWORD(6,I)
      IPAIR(2,1) = JWORD(3,I)
      IPAIR(2,2) = JWORD(5,I)
      GO TO 53
   51 IPAIR(1,1)= JWORD(1,I)
      IPAIR(1,2) = JWORD(2,I)
      IPAIR(2,1) = JWORD(3,I)
      IPAIR(2,2) = JWORD(4,I)
      GO TO 53
   52 IPAIR(1,1) = JWORD(1,I)
      IPAIR(1,2) = JWORD(3,I)
      IPAIR(2,1) = JWORD(2,I)
      IPAIR(2,2) = JWORD(4,I)
*
*      TEST WHETHER RANGE OF SUMMATION HAS BEEN DEFINED. WE CHOOSE THE
*      FIRST PAIR OF J VALUES THAT DEFINE THE RANGE AND STORE IN JSUM
*
   53 DO 54 I2=1,2
      DO 55 I3=1,2
      IF(IPAIR(I2,I3)-M) 55,55,56
   56 I4 = IPAIR(I2,I3)-M
*
*      JSUM7 GREATER THAN ZERO MEANS THAT LIMIT IS DEFINED PREVIOUSLY
*
      IF(JSUM7(I4)) 54,54,55
   55 CONTINUE
      GO TO 57
   54 CONTINUE
      GO TO 44
*
*      NCT IS COUNT ON ORDER OF SUMMATION
*
   57 NCT = NCT+1
      JSUM7(I1)=NCT
*
*      JSUM(I,J),I=1,2,J=1,NSUM CONTAINS THE POSITION OF THE J VALUES
*      THAT DEFINE THE RANGE OF EACH VARIABLE. THE FIRST ROW CORRESPONDS
*      TO THE FIRST J AND THE SECOND ROW TO THE SECOND J DEFINING RANGE.
*      IF VALUE IN RANGE 1 TO M THEN CORRESPONDS TO AN ELEMENT IN J1.
*      IF VALUE GREATER THAN M THEN CORRESPONDS TO A SUMMATION VARIABLE
*      IN JSUM1 LIST. NOTE THAT JSUM DOES NOT NECESSARILY CONTAIN THE
*      MOST RESTRICTIVE RANGES SINCE ONLY ONE OF TWO POSSIBLE PAIRS FROM
*      THE RACAH COEFFICIENT IS TAKEN
*
      DO 58 I3=1,2
      JSUM(I3,I1)=IPAIR(I2,I3)
   58 CONTINUE
   44 CONTINUE
   43 CONTINUE
*
*      CHECK WHETHER THE RANGE OF ALL SUMMATIONS SET. FAIL IF NOT
*      POSSIBLE TO SET ALL RANGES
*
      IF(NCT-NSUM) 59,60,60
   59 IF(NCT-NCT1) 61,61,62
   61 WRITE(IWRITE,63)
      STOP
   62 NCT1=NCT
      GO TO 64
*
*      JSUM8(I),I=1,NSUM IS THE POSITION IN THE JSUM7 LIST WHERE THE ITH
*      SUMMATION IS FOUND
*
   60 DO 65 J=1,NSUM
      DO 66 I1=1,NSUM
      IF(JSUM7(I1)-J) 66,67,66
   66 CONTINUE
      I1=NSUM
   67 JSUM8(J)=I1
   65 CONTINUE
*
*
*
*      S E C T I O N  4
*
*      CARRY OUT THE SUMMATIONS.
*      I6 DENOTES THE FIRST J THAT REQUIRES TO BE SET TO THE LOWEST
*      VALUE IN THE RANGE
*      I7 = 0 THE FIRST TIME THE JS ARE SET BUT BUT IS SET EQUAL TO 1
*      ON SUBSEQUENT TIMES
*
      I6=1
      I7=0
  100 IF(I6-NSUM) 105,105,104
*
*      JSUM2(I),I=1,NSUM CONTAINS CURRENT VALUE OF (2J+1) IN THE SAME
*      ORDER AS JSUM1 LIST. SET JSUM2 EQUAL TO LOWEST VALUE IN EACH
*      RANGE
*
  105 DO 68 J=I6,NSUM
      I1=JSUM8(J)
      IF(JSUM(1,I1)-M) 69,69,70
*
*      FIRST J DEFINING RANGE FIXED
*
   69 I2=JSUM(1,I1)
      I3=J1(I2)
      GO TO 71
*
*      FIRST J DEFINING RANGE VARIABLE
*
   70 I2=JSUM(1,I1)-M
      I3=JSUM2(I2)
   71 IF(JSUM(2,I1)-M) 72,72,73
*
*      SECOND J DEFINING RANGE FIXED
*
   72 I2=JSUM(2,I1)
      I4=J1(I2)
      GO TO 74
*
*      SECOND J DEFINING RANGE VARIABLE
*
   73 I2=JSUM(2,I1)-M
      I4=JSUM2(I2)
*
*      SET LOWER LIMIT OF RANGE IN JSUM2
*
   74 JSUM2(I1)=IABS(I3-I4)+1
   68 CONTINUE
*
*      JSUM3(I),I=1,NSUM IS 1 IF J HAS ALTERED FROM ITS PREVIOUS VALUE
*      AND IS 0 IF IT IS STILL THE SAME
*
      DO 77 I=I6,NSUM
      JSUM3(I)=1
   77 CONTINUE
      IF(I7) 103,103,104
  103 I7=1
*
*      JWTEST(I),I=1,JWRD IS 1 IF REQUIRED TO EVALUATE RACAH COEFFICIENT
*      AND IS 0 IF VALUE THE SAME AS BEFORE.JWTEST IS SET ZERO THE FIRST
*      TIME THROUGH AND LATER SET 1 IF NECESSARY
*
      DO 78 I=1,JWRD
      JWTEST(I)=0
   78 CONTINUE
*
*      STOR1 WILL CONTAIN THE PRODUCT OF RACAH COEFFICIENTS TIMES
*      (2J+1) FACTORS
*      STOR WILL CONTAIN SUMS OF THE STOR1
*
      STOR1=1.D0
      STOR=0.D0
*
*      CHECK THE TRIANGULAR RELATION FOR ALL J VALUES IN JWORD LIST. IF
*      A SUMMATION VARIABLE THEN VALUE TAKEN FROM JSUM2 LIST
*
  104 DO 79 J=1,JWRD
      DO 80 I=1,6
      IF(JWORD(I,J)-M) 81,81,82
   81 I1=JWORD(I,J)
      IST(I) = J1(I1) - 1
      GO TO 80
   82 I1=JWORD(I,J)-M
      IST(I) = JSUM2(I1) - 1
   80 CONTINUE
      IF(IST(1)+IST(2)-IST(5)) 83,84,84
   84 IF(IABS(IST(1)-IST(2))-IST(5)) 85,85,83
   85 IF(IST(3)+IST(4)-IST(5)) 83,86,86
   86 IF(IABS(IST(3)-IST(4))-IST(5)) 87,87,83
   87 IF(IST(1)+IST(3)-IST(6)) 83,88,88
   90 IF(IABS(IST(2)-IST(4))-IST(6)) 79,79,83
   89 IF(IST(2)+IST(4)-IST(6)) 83,90,90
   88 IF(IABS(IST(1)-IST(3))-IST(6))89,89,83
   79 CONTINUE
      GO TO 91
*
*      FAIL ONE OF THE TRIANGULAR RELATIONS. INCREASE THE J VALUES
*
   83 I2=NSUM
  203 I1 = JSUM8(I2)
*
*      INCREASE A SUMMATION J VALUE WHICH IS IN JSUM2 AND SET JSUM3 TO
*      SHOW VALUE CHANGED
*
      JSUM2(I1)=JSUM2(I1)+2
      JSUM3(I1)=1
*
*      NOW STORE J VALUE  DEFINING RANGE OF THIS J IN I3 AND I4.
*
      IF(JSUM(1,I1)-M) 92,92,93
   92 I20 = JSUM(1,I1)
      I3 = J1(I20)
      GO TO 94
   93 I20 = JSUM(1,I1)-M
      I3 = JSUM2(I20)
   94 IF(JSUM(2,I1)-M)95,95,96
   95 I20 = JSUM(2,I1)
      I4 = J1(I20)
      GO TO 97
   96 I20 = JSUM(2,I1)-M
      I4 = JSUM2(I20)
   97 I5=I3+I4-1
   98 I6=I2+1
*
*      NOW TEST J VALUES AGAINST MAXIMUM IN RANGE. IF SATISFIED RETURN
*      TO SET REMAINING J VALUES WHICH DEPEND ON THIS J TO THEIR
*      LOWEST VALUES. IF NOT RETURN TO INCREASE PRECEDING J VALUE
*
      IF(JSUM2(I1)-I5) 100,100,101
  101 I2=I2-1
      IF(I2) 102,102,203
*
*      NO MORE J VALUES TO SUM OVER. THE SUMMATION IS THEREFORE COMPLETE
*      MULTIPLY BY COMMON FACTOR AND EXIT
*
  102 RECUP=RECUP*STOR
      IF(IBUG3-1) 131,230,131
  230 WRITE(IWRITE,169) RECUP,STOR,STOR1
  131 RETURN
*
*      SEE TRIANGULAR RELATIONS ARE SATISFIED. NOW PROCEED TO EVALUATE
*      RACAH COEFFICIENTS
*      FIRST DETERMINE WHICH RACAH COEFFICIENTS NEED RE-EVALUATING AND
*      SET JWTEST APPROPRIATELY
*
   91 DO 106 J=1,NSUM
      IF(JSUM3(J)) 106,106,107
  107 I2=JSUM6(J)
      DO 108 I1=1,I2
      I3=JSUM5(J,I1)
      JWTEST(I3)=1
  108 CONTINUE
  106 CONTINUE
*
*      NOW EVALUATE ALL JWRD RACAH COEFFICIENTS WHICH HAVE NOT ALREADY
*      BEEN EVALUATED
*
      DO 109 I=1,JWRD
      IF(JWTEST(I)) 109,109,110
  110 DO 111 I1=1,6
      IF(JWORD(I1,I)-M) 112,112,113
  112 I2=JWORD(I1,I)
      IST(I1) = J1(I2) - 1
      GO TO 111
  113 I2=JWORD(I1,I)-M
      IST(I1) = JSUM2(I2) - 1
  111 CONTINUE
      IF(IBUG3-1) 132,133,132
  133 WRITE (IWRITE,170) (IST(J), J=1,6)
  132 CALL GRACAH(IST(1),IST(2),IST(3),IST(4),IST(5),IST(6),X1)
      WSTOR(I)=X1
  109 CONTINUE
*
*      WSTOR(I),I=1,JWRD CONTAINS THE EVALUATED RACAH COEFFICIENTS
*
      IF(IBUG3-1) 134,135,134
  135 WRITE(IWRITE,190) (WSTOR(J),J=1,JWRD)
*
*      SET JSUM3 AND JWTEST TO ZERO TO INDICATE THAT RACAH COEFFICIENTS
*      NEED NOT BE EVALUATED UNLESS J VALUE CHANGES
*
  134 DO 114 J=1,NSUM
      JSUM3(J)=0
  114 CONTINUE
      DO 115 J=1,JWRD
      JWTEST(J)=0
  115 CONTINUE
*
*      FORM PRODUCT OF RACAH COEFFICIENTS,(2J+1) FACTORS AND (-1)
*      FACTORS IN STOR1
*
      DO 116 I=1,JWRD
      STOR1 = STOR1*WSTOR(I)
  116 CONTINUE
*
*      IASTOR CONTAINS THE POWER OF (-1)WHICH IS COMMON TO ALL TERMS
*
      IX2 = IASTOR
      IF(J6CP) 117,117,118
  118 DO 119 I=1,J6CP
      I1=J6P(I)
      STOR1 = STOR1*DSQRT(DFLOAT(JSUM2(I1)))
  119 CONTINUE
  117 IF(J7CP) 120,120,121
  121 DO 122 I=1,J7CP
      I1=J7P(I)
      IX2 = IX2 + JSUM2(I1) - 1
  122 CONTINUE
  120 IF(J8CP) 123,123,124
  124 DO 125 I=1,J8CP
      I1=J8P(I)
      IX2 = IX2 - JSUM2(I1) + 1
  125 CONTINUE
  123 IX2 = IX2/2
*
*      ADD TERM INTO STOR AND RESET STOR1 TO 1 READY FOR NEXT TERM
*
      IF (MOD(IX2,2) .EQ. 1) STOR1 = -STOR1
      STOR = STOR + STOR1
      STOR1=1.D0
      GO TO 83
*
*      NO SUMMATIONS. CHECK THAT THERE ARE NO INCONSISTENCIES. THEN
*      MULTIPLY BY (-1) FACTOR AND EXIT
*
   12 IF(J6CP+J7CP+J8CP) 309,309,310
  310 WRITE(IWRITE,311)
      STOP
  309 IX2 = IASTOR/2
      IF (MOD(IX2,2) .EQ. 1) RECUP = -RECUP
  186 RETURN
      END
*
*     ------------------------------------------------------------------
*	N J S Y M
*     ------------------------------------------------------------------
*
      SUBROUTINE NJSYM (J6C,J7C,J8C,JWC,K6,K7,K8,KW,RECUP)
*
      IMPLICIT REAL *8(A-H,O-Z)
*
*
*      GENERAL RECOUPLING PROGRAMME
*      EVALUATES THE RECOUPLING COEFFICIENT RECUP BETWEEN TWO COUPLING
*      SCHEMES
*
*
*      J6C            THE NUMBER OF ELEMENTS IN THE K6 ARRAY
*      J7C            THE NUMBER OF ELEMENTS IN THE K7 ARRAY
*      J8C            THE NUMBER OF ELEMENTS IN THE K8 ARRAY
*      JWC            THE NUMBER OF COLUMNS IN THE KW ARRAY
*      K6(I),I=1,J6C. EACH ENTRY CORRESPONDS TO A FACTOR DSQRT(2J+1) IN
*                     RECUP. THE VALUE OF K6 GIVES POSITION IN J1 ARRAY
*                     WHERE J VALUE IS FOUND
*      K7(I),I=1,J7C. EACH ENTRY CORRESPONDS TO A FACTOR (-1)**J IN
*                     RECUP
*      K8(I),I=1,J8C. EACH ENTRY CORRESPONDS TO A FACTOR (-1)**(-J) IN
*                     RECUP
*      KW(I,J),I=1,6,J=1,JWC.     EACH COLUMN CORRESPONDS TO A RACAH
*                     COEFFICIENT IN RECUP
*      RECUP          THE RESULTANT  RECOUPLING COEFFICIENT EVALUATED
*                     AND STORED IN RECUP
*
*
*      THE ARRAYS K6,K7,K8 AND KW ARE EVALUATED BY NJSYM. THE ENTRY IN
*      EACH CASE CORRESPONDS TO A POSITION IN THE J1 ARRAY WHERE THE
*      2J+1 VALUE IS FOUND IF LESS THAN OR EQUAL TO M,OR TO A SUMMATION
*      VARIABLE IF GREATER THAN M
*
*      THE SUMMATION OVER THE VARIABLES IN K6,K7,K8 AND KW AND THE
*      EVALUATION OF RECUP IS CARRIED OUT IN GENSUM
*
*      GENSUM CAN BE RE-ENTERED DIRECTLY TO EVALUATE DIFFERENT
*      RECOUPLING COEFFICIENTS WITH THE SAME STRUCTURE BY JUST ALTERING
*      THE NUMBERS IN THE J1 ARRAY
*
      DIMENSION K6(60),K7(80),K8(60),KW(6,20)
      COMMON/COUPLE/M,N,J1(60),J2(18,3),J3(18,3)
      COMMON/DEBUG/IBUG1,IBUG2,IBUG3,NBUG6,NBUG7,IFULL
      COMMON/DEPTHS/J4(60),J5(60)
      COMMON/DIMEN/KFL1,KFL2,KFL3,KFL4,KFL5,KFL6,KFL7,MXIHSH
      COMMON/INFORM/IREAD,IWRITE,IOUT,ISC1,ISC2,ISC3,JSC0,JSC1,JSC2,JSC3
      COMMON/WCOMI9/I3,I4,I5,I6,I7,I8,I9,I17,I18,I19,I20
*
*      FORMAT STATEMENTS USED IN NJSYM
*
   50 FORMAT(59H RECOUPLING COEFFICIENT SET ZERO AS TRIANGLE DOES NOT MA
     :TCH)
   65 FORMAT(29H FAIL IN RECOUPLING PROGRAMME)
  107 FORMAT(4H J1=,20I5)
  108 FORMAT(23H J2                  J3)
  110 FORMAT(3I5,I10,2I5)
  111 FORMAT(3H KW)
  112 FORMAT(6I5)
  113 FORMAT(4H K6=,38I3)
  114 FORMAT(4H K7=,38I3)
  115 FORMAT(4H K8=,38I3)
  145 FORMAT(8H JWC = 0,8H J6C = 0,8H J7C = 0,8H J8C = 0)
  204 FORMAT(23H KFL2 DIMENSION FAILURE)
  207 FORMAT(23H KFL3 DIMENSION FAILURE)
  208 FORMAT(23H KFL4 DIMENSION FAILURE)
  209 FORMAT(23H KFL5 DIMENSION FAILURE)
  221 FORMAT(17H NO KW ARRAYS SET)
  226 FORMAT(17H NO K6 ARRAYS SET)
  230 FORMAT(17H NO K7 ARRAYS SET)
  233 FORMAT(17H NO K8 ARRAYS SET)
 1208 FORMAT(23H KFL7 DIMENSION FAILURE)
*
*      SET DIMENSIONVARIABLES AND TEST SOME OF DIMENSIONS
*
  201 IF(KFL2-N+1) 202,203,203
  202 WRITE(IWRITE,204)
      STOP
  203 IF(KFL7-M) 205,206,206
  205 WRITE(IWRITE,1208)
      STOP
*
*      IP IS THE NUMBER OF INEQUIVALENT TRIADS WHICH HAVE TO BE
*      RECOUPLED. IT IS SET INITIALLY TO THE TOTAL NUMBER OF TRIADS AND
*      THEN DECREASED IN SECTION 1 BELOW AS THE RECOUPLING PROCEEDS
*      UNTIL EVENTUALLY IT REACHES ZERO
*
  206 IP=N-1
*
*      DEBUG PRINTS
*
      IF(IBUG3-1)124,123,124
  123 WRITE(IWRITE,108)
      DO 125 I=1,IP
      WRITE(IWRITE,110) (J2(I,J),J=1,3),(J3(I,J),J=1,3)
  125 CONTINUE
*
*      SET COUNTS ZERO. MP IS COUNT ON THE J VALUES WHICH ARE SUMMED
*      OVER.
*
  124 J6C=0
      J7C=0
      J8C=0
      JWC=0
      MP=M
*
*
*
*
*
*     S E C T I O N  1
*
*      THE FOLLOWING SECTION SEARCHES THE J2 AND J3 ARRAYS TO SEE IF
*      ANY TRIADS ARE EQUIVALENT. IF SO IT PUTS THEM AT END OF J2 AND J3
*      ARRAYS AND SETS IP EQUAL TO THE NUMBER OF INEQUIVALENT TRIADS
*      REMAINING. IF IP=0 THEN THE RECOUPING HAS BEEN COMPLETED AND EXIT
*      IS MADE TO GENSUM TO CARRY OUT THE SUMMATIONS
*
  117 I1=1
   16 DO  1 I2=1,IP
      IF(J2(I2,1)-J3(I1,1)) 2,3,2
    2 IF(J2(I2,2)-J3(I1,1))1,4,1
    1 CONTINUE
*
*      NO EQUIVALENT TRIADS WITH THIS VALUE OF I1. INCREASE I1 AND TRY
*      AGAIN
*
      GO TO 5
    3 IF(J2(I2,2)-J3(I1,2))5,6,5
    4 IF(J2(I2,1)-J3(I1,2))5,6,5
    6 IF(I2-IP)7,8,8
*
*      REARRANGE SO THAT EQUIVALENT TRIADS OCCUR AT THE END OF J2 AND
*      J3 ARRAYS
*
    7 I3=J2(I2,1)
      I4=J2(I2,2)
      I5=J2(I2,3)
      I6=I2+1
      DO 9 I7=I6,IP
      DO 10 I8=1,3
      J2(I7-1,I8)=J2(I7,I8)
   10 CONTINUE
    9 CONTINUE
      J2(IP,1)=I3
      J2(IP,2)=I4
      J2(IP,3)=I5
    8 IF(I1-IP)11,14,14
   11 I3=J3(I1,1)
      I4=J3(I1,2)
      I5=J3(I1,3)
      I6=I1+1
      DO 12 I7=I6,IP
      DO 13 I8=1,3
      J3(I7-1,I8)=J3(I7,I8)
   13 CONTINUE
   12 CONTINUE
      J3(IP,1)=I3
      J3(IP,2)=I4
      J3(IP,3)=I5
*
*      IS THE THIRD ELEMENT IN J2 SUMMED OVER. IF SO REPLACE BY THIRD
*      ELEMENT IN J3 ARRAY
*
   14 IF(J2(IP,3)-M)47,47,44
   44 J=J3(IP,3)
      JP=J2(IP,3)
      J2(IP,3)=J
      IF(IP-2)101,18,18
*
*      NOW REPLACE ALL OTHER ELEMENTS IN J2,KW,K7,K8 AND K6 WHICH ARE
*      SUMMED OVER AT THE SAME TIME BY THE SAME QUANTITY J
*
   18 IQ=IP-1
      DO 19 I3=1,IQ
      DO 20 I4=1,3
      IF(J2(I3,I4)-JP) 20,21,20
   21 J2(I3,I4)=J
   20 CONTINUE
   19 CONTINUE
  101 IF(JWC)38,38,39
   39 DO 23 I=1,6
      DO 22 I3=1,JWC
      IF(KW(I,I3)-JP) 22,25,22
   25 KW(I,I3)=J
   22 CONTINUE
   23 CONTINUE
   38 IF(J7C)87,87,41
   41 DO 34 I3=1,J7C
      IF(K7(I3)-JP)34,35,34
   35 K7(I3)=J
   34 CONTINUE
   87 IF(J8C)40,40,86
   86 DO 88 I3=1,J8C
      IF(K8(I3)-JP)88,89,88
   89 K8(I3)=J
   88 CONTINUE
   40 IF(J6C)42,42,43
   43 DO 36 I3=1,J6C
      IF(K6(I3)-JP)36,37,36
   37 K6(I3)=J
   36 CONTINUE
*
*      SET I1 BACK TO 1 IN ORDER TO START SEARCH FOR EQUIVALENT TRIADS
*      AGAIN SINCE SOME ELEMENTS MAY HAVE BEEN ALTERED
*
   42 I1=1
*
*      TEST WHETHER TRIANGLE MATCHES
*
   47 JJ2=J2(IP,3)
      JJ3=J3(IP,3)
      IF(JJ2-JJ3) 148,49,148
  148 IF(J1(JJ2)-J1(JJ3)) 48,44,48
*
*      RECOUPLING COEFFICIENT SET ZERO WHEN TRIAD IN INITIAL AND FINAL
*      STATES DO NOT MATCH.  IN THIS CASE, GENSUM IS NOT CALLED AND THE
*     ARRAYS K6,K7,K8,KW  ARE NOT SET UP, READY FOR FURTHER DIRECT
*      ENTRIES TO GENSUM.
*
   48 IF(IBUG3-1) 150,151,150
  151 WRITE(IWRITE,50)
  150 RECUP=0.D0
      RETURN
*
*      IF J2 ANGULAR MOMENTA ARE IN OPPOSITE ORDER TO J3 ANGULAR
*      MOMENTA INTERCHANGE THEM AND STORE SIGN CHANGES IN K7 AND K8.
*      CHECK DIMENSIONS
*
   49 IF(J2(IP,1)-J3(IP,1))100,99,100
  100 J=J2(IP,1)
      J2(IP,1)=J2(IP,2)
      J2(IP,2)=J
       K7(J7C+1)=J2(IP,1)
      K7(J7C+2)=J2(IP,2)
      J7C=J7C+2
      K8(J8C+1)=J2(IP,3)
      J8C=J8C+1
      IF(KFL5-J7C) 210,220,220
  220 IF(KFL4-J8C) 212,99,99
*
*      DECREASE IP AND RETURN TO LOOK FOR FURTHER EQUIVALENT TRIADS
*
   99 IP=IP-1
      GO TO 15
    5 I1=I1+1
   15 IF(I1-IP)16,16,17
*
*      IF IP = 0 THIS MEANS THAT ALL TRIADS HAVE BEEN TRANSFORMED TO BE
*      EQUIVALENT. NOW EXIT TO SUM OVER RACAH COEFFICIENTS
*
   17 IF(IP)126,126,46
*
*
*
*
*
*     S E C T I O N  2
*
*      ITEST = 0      DETERMINES THE MIMIMUM RECOUPLING OF J2 ARRAY TO
*      OBTAIN AN EQUIVALENT TRIAD TO ONE IN J3 ARRAY. STORE ROW OF J3
*      ARRAY IN ITEST1.
*      ITEST = 1      DETERMINE RECOUPLING OF J2 ARRAY TO OBTAIN AN
*      EQUIVALENT TRIAD OF ITEST1 ROW OF J3 ARRAY.
*      IN BOTH CASES STORE INFORMATION ON RECOUPLING
*
   46 I10=9999
      ITEST=0
      I1=1
*
*      GENJ45 DETERMINES THE LEVEL OF EACH J IN THE COUPLING TREE OF J2
*      AND J3 AND STORES THE RESULT IN THE J4 AND J5 ARRAYS RESPECTIVELY
*
   96 CALL GENJ45(IP)
*
*      LOOK FOR J IN J2 ARRAY WHICH IS SAME AS FIRST ELEMENT IN J3 ARRAY
*
   95 DO 52 I2=1,IP
      IF(J2(I2,1)-J3(I1,1))53,54,53
   53 IF(J2(I2,2)-J3(I1,1))52,55,52
   52 CONTINUE
      GO TO 51
*
*      I3 AND I5 DENOTES POSITION IN J2 ARRAY OF COMMON J
*
   54 I3=1
      GO TO 60
   55 I3=2
   60 I5=I2
*
*      NOW LOOK FOR J IN J2 ARRAY WHICH IS SAME AS OTHER ELEMENT IN J3
*      ARRAY
*
      DO 56 I2=1,IP
      IF(J2(I2,1)-J3(I1,2))57,58,57
   57 IF(J2(I2,2)-J3(I1,2))56,59,56
   56 CONTINUE
      GO TO 51
*
*      I4 AND I6 DENOTES POSITION IN J2 ARRAY OF COMMON J
*
   58 I4=1
      GO TO 61
   59 I4=2
   61 I6=I2
*
*      I7 AND I8 DENOTE THE POSITION IN THE J1 ARRAY OF THE TWO COMMON J
*      VALUES IN J2 AND J3
*
      I7=J2(I5,I3)
      I8=J2(I6,I4)
*
*      GENI9 DETERMINES THE NUMBER OF RECOUPLINGS OF TWO ELEMENTS OF J2
*      NECESSARY TO OBTAIN IDENTICAL TRIADS IN J2 AND J3 ARRAYS. THIS
*      NUMBER PLUS TWO IS STORED IN I9
*
      CALL GENI9(IP)
      IF(I9-I10)62,51,51
*
*      A SMALLER RECOUPLING PAIR FOUND. STORE LOWEST AS J2(I13,I14) AND
*      HIGHEST AS J2(I11,I12). I15 AND I16 CONTAIN LEVEL OF THESE BELOW
*      COMMON TRIADS. FINALLY ITEST1 DENOTES TRIAD IN J3 FOR NEXT ENTRY
*      TO SECTION 2 AND IS REQUIRED IF MORE THAN ONE RECOUPLING
*
   62 I10=I9
      I11 = I17
      I12 = I19
      I13 = I18
      I14 = I20
      I15=I7
      I16=I8
      ITEST1=I1
   51 IF(ITEST) 98,97,98
*
*      I1 IS ONLY INCREASED IF SEARCHING FOR SMALLEST RECOUPLING PAIR
*
   97 I1=I1+1
      IF(I1-IP)95,95,98
   98 IF(I10-9999)63,64,64
*
*      FAIL BECAUSE NO PAIR IN J2 AND J3 FOUND WHICH COULD BE RECOUPLED
*
   64 WRITE(IWRITE,65)
      STOP
*
*
*
*
*
*     S E C T I O N  3
*
*      THE PAIR OF J VALUES THAT REQUIRE THE SMALLEST NUMBER OF
*      RECOUPLINGS OF J2 TO BRING INTO THE SAME ORDER AS J3 HAS NOW
*      BEEN FOUND. THIS SECTION NOW CARRIES OUT ONE RECOUPLING
*
   63 IF(I15-I16) 67,68,68
*
*      I1 AND I2 DENOTES THE LEVEL ABOVE THE GIVEN LEVELS OF THE TRIAD
*      OF ELEMENTS TO BE RECOUPLED
*
   67 I1=I15-1
      I2=I16-2
      GO TO 69
   68 I1=I16-1
      I2=I15-2
   69 I3 = I11
      I4 = I13
      I5 = I12
      I6 = I14
      IF(I1)70,70,71
*
*      FIND FIRST ELEMENT TO BE RECOUPLED
*
   71 DO 72 I=1,I1
      DO 73 I7=1,IP
      IF(J2(I7,1)-J2(I3,3))74,75,74
   74 IF(J2(I7,2)-J2(I3,3)) 73,76,73
   73 CONTINUE
   75 I5=1
      GO TO 77
   76 I5=2
   77 I3=I7
   72 CONTINUE
*
*      FIRST ELEMENT TO BE RECOUPLED IS J2(I3,I5)
*      NOW FIND SECOND ELEMENT TO BE RECOUPLED
*
   70 IF(I2)78,78,79
   79 DO 80 I=1,I2
      DO 81 I7=1,IP
      IF(J2(I7,1)-J2(I4,3))82,83,82
   82 IF(J2(I7,2)-J2(I4,3))81,84,81
   81 CONTINUE
   83 I6=1
      GO TO 85
   84 I6=2
   85 I4=I7
   80 CONTINUE
*
*      SECOND ELEMENT TO BE RECOUPLED IS J2(I4,I6)
*
   78 IF(I6-1)90,90,91
*
*      INTERCHANGE ELEMENTS OF I4 ROW OF J2 IF NECESSARY AND INCLUDE
*      SIGNS IN K7 AND K8 ARRAYS
*
   90 K7(J7C+1)=J2(I4,1)
      K7(J7C+2)=J2(I4,2)
      J7C=J7C+2
      K8(J8C+1)=J2(I4,3)
      J8C=J8C+1
      I=J2(I4,1)
      J2(I4,1)=J2(I4,2)
      J2(I4,2)=I
   91 IF(I5-1) 92,92,93
*
*      INTERCHANGE ELEMENTS OF I3 ROW OF J2 IF NECESSARY AND STORE SIGNS
*      IN K7 AND K8 ARRAYS
*
   92 K7(J7C+1)=J2(I3,1)
      K7(J7C+2)=J2(I3,2)
      J7C=J7C+2
      K8(J8C+1)=J2(I3,3)
      J8C=J8C+1
      I=J2(I3,1)
      J2(I3,1)=J2(I3,2)
      J2(I3,2)=I
*
*      NOW RECOUPLE THE TWO ELEMENTS OF J2 AND STORE SQUARE ROOTS IN K6
*      AND RACAH COEFFICIENT IN KW ARRAYS. MP DENOTES A J WHICH WILL BE
*      SUMMED OVER
*
   93 K6(J6C+1)=J2(I4,3)
      MP=MP+1
      K6(J6C+2)=MP
      J6C=J6C+2
      JWC=JWC+1
      KW(1,JWC)=J2(I4,1)
      KW(2,JWC)=J2(I4,2)
      KW(3,JWC)=J2(I3,3)
      KW(4,JWC)=J2(I3,2)
      KW(5,JWC)=J2(I3,1)
      KW(6,JWC)=MP
      J2(I3,1)=J2(I4,1)
      J2(I4,1)=J2(I4,2)
      J2(I4,2)=J2(I3,2)
      J2(I4,3)=MP
      J2(I3,2)=MP
*
*      TEST DIMENSIONS AND EXIT IF FAILURE
*
      IF(KFL5-J7C)210,211,211
  210 WRITE(IWRITE,209)
      STOP
  211 IF(KFL4-J8C) 212,213,213
  212 WRITE(IWRITE,208)
      STOP
  213 IF(KFL4-J6C) 212,217,217
  217 IF(KFL3-JWC) 218,219,219
  218 WRITE(IWRITE,207)
      STOP
  219 IF(I1+I2) 117,117,94
*
*      MORE THAN ONE RECOUPLING REQUIRED. RETURN TO SECTION 2 TO DECIDE
*      WHICH ELEMENTS OF J2 TO RECOUPLE IN NEXT STEP. IF ALL RECOUPLINGS
*      OF A PARTICULAR PAIR HAVE BEEN CARRIED OUT THEN IDENTICAL PAIRS
*      ARE NOW PRESENT IN J2 AND J3 ARRAYS. RETURN TO SECTION 1 TO SEE
*      IF ANY MORE RECOUPLING REQUIRED
*
   94 ITEST=1
      I1=ITEST1
      I10=9999
      GO TO 96
*
*      DEBUG PRINTS
*
  126 IF(IBUG3-1) 105,104,105
  104 WRITE(IWRITE,107) (J1(I),I=1,M)
      WRITE(IWRITE,111)
      IF(JWC) 127,127,128
  128 DO 116 J=1,JWC
      WRITE(IWRITE,112) (KW(I,J),I=1,6)
  116 CONTINUE
      GO TO 224
  127 WRITE(IWRITE,221)
  224 IF(J6C) 222,222,223
  223 WRITE(IWRITE,113) (K6(J),J=1,J6C)
      GO TO 225
  222 WRITE(IWRITE,226)
  225 IF(J7C) 227,227,228
  228 WRITE(IWRITE,114) (K7(J),J=1,J7C)
      GO TO 229
  227 WRITE(IWRITE,230)
  229 IF(J8C) 231,231,232
  232 WRITE(IWRITE,115) (K8(J),J=1,J8C)
      GO TO 105
  231 WRITE(IWRITE,233)
*
*      CARRY OUT SUMMATIONS
*
  105 CALL GENSUM(J6C,J7C,J8C,JWC,K6,K7,K8,KW,RECUP)
      RETURN
      END
