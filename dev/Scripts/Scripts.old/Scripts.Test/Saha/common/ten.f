*
*     ------------------------------------------------------------------
*	T E N S O R
*     ------------------------------------------------------------------
*
      SUBROUTINE TENSOR(KA,KB,ISPIN,IRHO,ISIG,VSHELL)
*
      IMPLICIT REAL *8(A-H,O-Z)
*
*
*     W. D. ROBB   -   NOVEMBER 1971
*
************************************************************************
*
*     A ROUTINE FOR THE EVALUATION OF ANGULAR AND SPIN FACTORS IN THE
*     REDUCED MATRIX ELEMENT OF ANY ONE-ELECTRON TENSOR OPERATOR BETWEEN
*     ARBITRARILY COUPLED L-S CONFIGURATIONS
*
*
*     **  NOTE THAT THE DEFINITIONS OF TENSOR OPERATORS USED ARE THOSE
*     OF FANO AND RACAH, IRREDUCIBLE TENSORIAL SETS, ACADEMIC PRESS 1959
*
************************************************************************
*
*                       DIMENSION STATEMENTS
*
      DIMENSION L6(60),L7(80),L8(60),LW(6,20),J2STO(10,3),
     :     J3STO(10,3),JMEM(5),VSHELL(20)
*
*                       COMMON BLOCKS
*
      COMMON/COUPLE/MN1,M0,J1(60),J2(18,3),J3(18,3)
      COMMON/INFORM/IREAD,IWRITE,IOUT,ISC1,ISC2,ISC3,JSC0,JSC1,JSC2,JSC3
      COMMON/MEDEFN/IHSH,NJ(10),LJ(10),NOSH1(10),NOSH2(10),J1QN1(19,3)
     :    ,J1QN2(19,3),IJFUL(10)
      COMMON/DEBUG/IBUG1,IBUG2,IBUG3,NBUG6,NBUG7,IFULL
      COMMON/TERMS/NROWS,ITAB(14),JTAB(14),NTAB(219)
*
  203 FORMAT(//7H NJ,LJ ,10(I6,I3))
  204 FORMAT(//6H NOSH ,10I4)
  205 FORMAT(//6H J1QN ,30I3)
  207 FORMAT(8F15.8)
  208 FORMAT(// 23H PARENT TERMS NOT FOUND//)
  209 FORMAT(//3H J1)
  210 FORMAT(24I5)
  211 FORMAT(24H J2                   J3)
  212 FORMAT(3I5,I10,2I5)
  213 FORMAT(///26H ORBITAL RECOUPLING COEFF=,D20.8)
  214 FORMAT(///23H SPIN RECOUPLING COEFF=,D20.8//)
  215 FORMAT(/28H THE CONTRIBUTION FROM SHELL,I2,3H IS,F15.8)
  216  FORMAT(//21H THIS IS NOT A PARENT)
  217 FORMAT(///8H VSHELL=,8F15.8)
  218 FORMAT(//24H FRACTIONAL PARENT TERMS,I2)
  219 FORMAT(//49H THE CONTRIBUTION FROM FRACTIONAL PARENTAGE TERMS,I2,
     : 3H IS,F15.8)
  220 FORMAT(//6H SHELL,I2)
  302 FORMAT(/5X,89H NO CONTRIBUTION FROM TENSOR SINCE MORE THAN ONE ELE
     :CTRON DIFFERENT IN THE CONFIGURATIONS/)
  303 FORMAT(/5X,114H NO CONTRIBUTION FROM TENSOR SINCE THE TRIANGLE REL
     :ATION BETWEEN KA AND THE TOTAL ANGULAR MOMENTA IS NOT SATISFIED/)
  313 FORMAT(19H SPECTATOR SUBSHELL,I3,69H HAS DIFFERENT QUANTUM NUMBERS
     : ON THE TWO SIDES OF THE MATRIX ELEMENT/)
*
      AJF=1.D0
      RML=0.D0
      RPL=0.D0
      NTOT=0
      DO 100 IS=1,IHSH
      VSHELL(IS)=0.D0
  100 CONTINUE
      IHSHP1=IHSH+1
      I2HSH=IHSH*2-1
*
*     PRINT OUT THE OCCUPATION AND COUPLING ARRAYS
*
      IF(NBUG6-1) 101,2,101
    2 WRITE(IWRITE,203) (NJ(I),LJ(I),I=1,IHSH)
      WRITE(IWRITE,204)(NOSH1(J),J=1,IHSH)
      WRITE(IWRITE,204)(NOSH2(J),J=1,IHSH)
      WRITE(IWRITE,205) ((J1QN1(J,K),K=1,3),J=1,I2HSH)
      WRITE(IWRITE,205) ((J1QN2(J,K),K=1,3),J=1,I2HSH)
*
*      TEST FOR AT MOST ONE ELECTRON DIFFERENCE IN CONFIGURATIONS
*
  101 NOSHUM=0
      DO 102 K=1,IHSH
      NOSHUM=NOSHUM+IABS(NOSH1(K)-NOSH2(K))
  102 CONTINUE
      IF(NOSHUM.GT.2) GO TO 300
*
*     TEST FOR TRIANGLE RELATION BETWEEN KA AND TOTAL ANGULAR MOMENTA
*
  103 IF(ISPIN.EQ.0) GO TO 198
      K=3
      KC=KB
      IF(ISPIN.EQ.2) GO TO 199
      IF(J1QN1(I2HSH,2).NE.J1QN2(I2HSH,2)) GO TO 183
      GO TO 199
  198 K=2
      KC=KA
      IF(J1QN1(I2HSH,3).NE.J1QN2(I2HSH,3)) GO TO 183
  199 LB=J1QN1(I2HSH,K)-1
      NB=J1QN2(I2HSH,K)-1
      MB=KC+KC
      BTST=TRITST(MB,LB,NB)
      IF(DABS(BTST).GT.1.D-14) GO TO 301
      IF(K.EQ.2.OR.ISPIN.LT.2) GO TO 104
      K=2
      KC=KA
      GO TO 199
*
*      DETERMINE IRHO AND ISIGMA, THE NUMBERS OF THE OCCUPIED SHELLS
*
  104 IRHO=0
      ISIG=0
      DO 105 J=1,IHSH
      NX=NOSH1(J)-NOSH2(J)+2
      GO TO (107,105,106),NX
  107 ISIG=J
      GO TO 105
  106 IRHO=J
  105 CONTINUE
      IF(IRHO.NE.0 ) GO TO 108
      IRHO=1
      ISIG=1
  108 MEMR = IRHO
*
*     THE BEGINNING OF THE LOOP OVER ALL SHELLS
*
  109 IF(IRHO.NE.ISIG) GO TO 309
      IF(NBUG6-1) 309,4,309
    4 WRITE(IWRITE,220) IRHO
  309 NTOT=NTOT+1
      L1=LJ(IRHO)+1
      L2=LJ(ISIG)+1
      AJF=DFLOAT(J1QN1(I2HSH,2))/DFLOAT(2*LJ(IRHO)+1)
      IF(ISPIN.EQ.1) AJF=J1QN1(I2HSH,3)/2.D0
      IF(ISPIN.EQ.2) AJF=AJF*J1QN1(I2HSH,3)/2.D0
*
*     CHECK THE DIAGONAL CHARACTER OF QUANTUM NUMBERS OF SPECTATOR
*     SHELLS
*
      DO 255 J=1,IHSH
      IF(J.EQ.IRHO.OR.J.EQ.ISIG) GO TO 255
      DO 256 KK=1,3
      IF(J1QN1(J,KK).NE.J1QN2(J,KK)) GO TO 257
  256 CONTINUE
  255 CONTINUE
      GO TO 258
  257 IF(NBUG7.EQ.1) WRITE(IWRITE,313) J
      IF(IRHO.NE.ISIG) GO TO 190
      GO TO 189
  258 IF(IRHO-ISIG) 120,111,120
*
*     FIND THE PARENT TERMS GIVEN BY ALLOWED J VALUES IN NTAB WITH IRHO
*
  111 NELCTS=NOSH1(IRHO)
      K1=NTAB1(NELCTS,L1)
      KK1=ITAB(K1)
      DO 112 JJ1=1,KK1
      IJK1=3*(JJ1-1)+JTAB(K1)
      DO 113 K=2,3
      IJKK=IJK1+K
      IF(K.EQ.3) GO TO 114
      LA=NTAB(IJKK)
      MA=2*LJ(IRHO)+1
      NA=J1QN1(IRHO,K)
      GO TO 115
  114 LA=NTAB(IJKK)-1
      MA=1
      NA=J1QN1(IRHO,K)-1
  115 ATST=TRITST(LA,MA,NA)
      IF(DABS(ATST).GT.1.D-14) GO TO 116
  117 IF(K-3) 113,118,113
  116 JMEM(JJ1)=0
      GO TO 112
  118 JMEM(JJ1)=1
  113 CONTINUE
  112 CONTINUE
*
*     PARENTAGE CHECK
*
  120 IF(IRHO-ISIG) 121,127,121
  121 NELCTS=NOSH1(IRHO)
      K1=NTAB1(NELCTS,L1)
      NELCTS=NOSH2(ISIG)
      K2=NTAB1(NELCTS,L2)
      KK1=ITAB(K1)
      KK2=ITAB(K2)
      DO 122 JJ1=1,KK1
      IJK1=3*(JJ1-1)+JTAB(K1)
      DO 123 K=2,3
      IJKK=IJK1+K
      MSAM1=NTAB(IJKK)-J1QN2(IRHO,K)
      IF(MSAM1.NE.0) GO TO 122
      IF(K.EQ.3) GO TO 124
  123 CONTINUE
  122 CONTINUE
      IF(NBUG6-1) 192,7,192
    7 WRITE(IWRITE,208)
      GO TO 192
  124 DO 125 JJ2=1,KK2
      IJK2=3*(JJ2-1)+JTAB(K2)
      DO 126 K=2,3
      IJKK=IJK2+K
      MSAM2=NTAB(IJKK)-J1QN1(ISIG,K)
      IF(MSAM2.NE.0) GO TO 125
      IF(K.EQ.3) GO TO 127
  126 CONTINUE
  125 CONTINUE
      IF(NBUG6-1) 192,8,192
    8 WRITE(IWRITE,208)
      GO TO 192
*
*     SET  J2  AND  J3 .  SAME FOR  L  AND  S
*
  127 M1=IHSH-2
      M2=2*M1+1
      M3=3*IHSH-1
      M4=M3+1
      M5=M3+2
      M10=M5+1
      MN1=M10+1
      J2(1,1)=M10
      J2(1,2)=MN1
      J2(1,3)=M5
      J2(2,1)=IRHO
      J2(2,2)=M5
      J2(2,3)=M3
      J3(1,1)=ISIG
      J3(1,2)=M10
      J3(1,3)=M4
      IF(IRHO-1) 128,129,128
  129 J2(3,1)=M3
      GO TO 130
  128 J2(3,1)=1
  130 IF(IRHO-2) 131,132,131
  132 J2(3,2)=M3
      GO TO 133
  131 J2(3,2)=2
  133 J2(3,3)=IHSHP1
      IF(ISIG-1) 134,135,134
  135 J3(2,1)=M4
      GO TO 136
  134 J3(2,1) = 1
  136 IF(ISIG-2) 137,138,137
  138 J3(2,2)=M4
      GO TO 139
  137 J3(2,2)=2
  139 J3(2,3)=2*IHSH
      IF(IHSH-3) 149,140,140
  140 DO 148 J=4,IHSHP1
      L=J-1
      J2(J,1)=M1+L
      J2(J,3)=M1+J
      J3(L,1)=M2+L
      J3(L,3)=M2+J
  141 IF(IRHO-L) 142,143,142
  143 J2(J,2)=M3
      GO TO 144
  142 J2(J,2)=L
  144 IF(ISIG-L) 145,146,145
  146 J3(L,2)=M4
       GO TO 148
  145 J3(L,2)=L
  148 CONTINUE
  149 M6=IHSHP1
      J3(M6,1)=M3-1
      J3(M6,2)=MN1
      J3(M6,3)=I2HSH
      IF(IHSH-1) 450,451,450
  451 J3(M6,1) = M4
      J3(M6,3) = M3
  450 DO 150 J=1,IHSHP1
      DO 151 K=1,3
      J2STO(J,K)=J2(J,K)
      J3STO(J,K)=J3(J,K)
  151 CONTINUE
  150 CONTINUE
*
*     RECOUPLING COEFFICIENTS
*
      JMEM1=J1QN1(IRHO,1)
      JMEM2=J1QN1(IRHO,2)
      JMEM3=J1QN1(IRHO,3)
      JMEM4=J1QN2(ISIG,1)
      JMEM5=J1QN2(ISIG,2)
      JMEM6=J1QN2(ISIG,3)
      IF(IRHO-ISIG) 154,152,154
*
*     BEGINNING OF LOOP OVER ALL PARENT TERMS
*
  152 JJ1=1
 1152 IF(NBUG6-1) 12,11,12
   11 WRITE(IWRITE,218) JJ1
   12 IF(JMEM(JJ1).EQ.1) GO TO 153
      IF(NBUG6-1) 186,16,186
   16 WRITE(IWRITE,216)
      GO TO 186
  153 IJK1=3*(JJ1-1)+JTAB(K1)
      NI1=NTAB(IJK1+1)
      NI2=NTAB(IJK1+2)
      NI3=NTAB(IJK1+3)
      J1QN2(IRHO,1)=NI1
      J1QN1(ISIG,1)=NI1
      J1QN2(IRHO,2)=NI2
      J1QN1(ISIG,2)=NI2
      J1QN2(IRHO,3)=NI3
      J1QN1(ISIG,3)=NI3
  154 K=2
      M7=M3-IHSH
      M9=M7+1
      M11=M3-1
      M12=IHSH-1
      RECUPS=1.D0
      M0=M6+1
*
*     SET UP THE J1 ARRAY FOR THE ANGULAR AND SPIN RECOUPLING
*     COEFFICIENTS
*
  155 IF(K-3) 156,157,157
  156 J1(M5)=2*LJ(IRHO)+1
      J1(M10)=2*LJ(ISIG)+1
      J1(MN1)=2*KA+1
      IF(ISPIN.EQ.1) J1(MN1)=1
      J1(M3)=JMEM2
      J1(M4)=JMEM5
      IF(IRHO.EQ.ISIG) GO TO 158
      J1(M3)=J1QN1(IRHO,K)
      J1(M4)=J1QN2(ISIG,K)
      GO TO 158
  157 J1(M5)=2
      J1(M10)=2
      J1(MN1)=KB+KB+1
      IF(ISPIN.EQ.0) J1(MN1)=1
      J1(M3)=JMEM3
      J1(M4)=JMEM6
      IF(IRHO.EQ.ISIG) GO TO 158
      J1(M3)=J1QN1(IRHO,K)
      J1(M4)=J1QN2(ISIG,K)
  158 DO 161 J=1,IHSH
      IF(IRHO-J) 160,159,160
  159 J1(J)=J1QN2(IRHO,K )
      GO TO 161
  160 J1(J)=J1QN1(J,K)
  161 CONTINUE
      IF(IHSH.EQ.1) GO TO 197
      DO 162 J=M6,M7
      J1(J)=J1QN1(J,K)
  162 CONTINUE
      DO 163 J=M9,M11
      JM12=J-M12
      J1(J)=J1QN2(JM12,K)
  163 CONTINUE
*
*     PRINT OUT THE J1,J2 AND J3 ARRAYS
*
  197 IF(NBUG6-1) 304,9,304
    9 IF(K-3) 165,164,164
  165 IF(NBUG6-1) 304,17,304
   17 WRITE(IWRITE,209)
      WRITE(IWRITE,210) (J1(J),J=1,MN1)
      WRITE(IWRITE,211)
      DO 166 I=1,IHSHP1
      WRITE(IWRITE,212) (J2(I,J),J=1,3),(J3(I,J),J=1,3)
  166 CONTINUE
  304 CONTINUE
*
*     EVALUATE ORBITAL AND SPIN RECOUPLING COEFFICIENTS
*
  164 CALL NJSYM(J6C,J7C,J8C,JWC,L6,L7,L8,LW,RECUP)
      RECUPS=RECUPS*RECUP
      IF(K-3) 167,170,170
  167 IF(NBUG6-1) 305,18,305
   18 WRITE(IWRITE,213) RECUP
  305 CONTINUE
  170 K=K+1
      DO 168 J=1,IHSHP1
      DO 169 KK=1,3
      J2(J,KK)=J2STO(J,KK)
      J3(J,KK)=J3STO(J,KK)
  169 CONTINUE
  168 CONTINUE
      IF(K.EQ.3) GO TO 155
      IF(NBUG6-1) 306,19,306
   19 WRITE(IWRITE,214) RECUP
*
*     FIRST FRACTIONAL PARENTAGE COEFFICIENT
*
  306 LIJ=LJ(IRHO)
      COEFP=1.D0
      IF(LIJ) 171,272,171
  171 N=NOSH1(IRHO)
      IV1=JMEM1
      IL1=(JMEM2-1)/2
      IS1= JMEM3
      IV2=J1QN2(IRHO,1)
      IL2=(J1QN2(IRHO,2)-1 )/2
      IS2=J1QN2(IRHO,3)
      CALL CFP(LIJ,N,IV1,IL1,IS1,IV2,IL2,IS2,COEFP)
      RECUPS=RECUPS*COEFP
  272 IF(IRHO-ISIG) 172,173,172
  172 IF(DABS(RECUPS).LT.1.D-14) GO TO 183
*
*     SECOND FRACTIONAL PARENTAGE COEFFICIENT
*
  173 LIJ=LJ(ISIG)
      COEFP=1.D0
      IF(LIJ) 176,176,174
  174 N=NOSH2(ISIG)
      IV1=JMEM4
      IL1=(JMEM5-1)/2
      IS1=JMEM6
      IV2=J1QN1(ISIG,1)
      IL2=(J1QN1(ISIG,2)-1)/2
      IS2=J1QN1(ISIG,3)
      CALL CFP(LIJ,N,IV1,IL1,IS1,IV2,IL2,IS2,COEFP)
  176 RECUPS=RECUPS*COEFP
      IF(DABS(RECUPS).LT.1.D-14.AND.IRHO.NE.ISIG) GO TO 183
*
*     PERMUTATION FACTOR
*
  175 IDELP=2
      IF(IRHO-ISIG) 177,181,179
  177 JRHO = IRHO+1
      DO 178 J=JRHO,ISIG
  178 IDELP=IDELP+NOSH1(J)
      GO TO 181
  179 JSIG = ISIG+1
      DO 180 J=JSIG,IRHO
  180 IDELP = IDELP+NOSH2(J)
  181 MINUS=(-1)**IDELP
*
*     MULTIPLICATIVE FACTOR
*
      IF(IRHO-ISIG) 182,185,182
  182 SQRN=DSQRT(DFLOAT(NOSH1(IRHO)*NOSH2(ISIG)))
      VALML=SQRN*RECUPS*DFLOAT(MINUS)
      GO TO 184
  183 VALML=0.D0
  184 RML = RML+VALML
*     RESULT STORED IN VSHELL
      IF(NTOT.EQ.0) NTOT=1
      VSHELL(NTOT)=RML*DSQRT(AJF)
      GO TO 190
  185 VALUML=RECUPS
      IF(NBUG6.NE.0) WRITE(IWRITE,219) JJ1,VALUML
      RPL = RPL+VALUML
  186 IF(IRHO.NE.ISIG)GO TO 1186
      JJ1=JJ1+1
      IF(JJ1.LE.KK1)GO TO 1152
 1186 J1QN1(IRHO,1)=JMEM1
      J1QN1(IRHO,2)=JMEM2
      J1QN1(IRHO,3)=JMEM3
      J1QN2(ISIG,1)=JMEM4
      J1QN2(ISIG,2)=JMEM5
      J1QN2(ISIG,3)=JMEM6
      ANL=DFLOAT(NOSH1(IRHO))*RPL
*
*      RESULTS STORED IN VSHELL
*
      IF(NTOT.EQ.0) NTOT=1
      VSHELL(NTOT)=ANL*DSQRT(AJF)
  194 IF(NBUG6-1) 189,196,189
  196 WRITE(IWRITE,215) IRHO,ANL
  189 IRHO=IRHO+1
      ISIG=ISIG+1
      RPL=0.D0
      IF(IRHO-IHSH) 109,109,190
  190 IF(NBUG6-1) 192,13,192
   13 WRITE(IWRITE,217) (VSHELL(N),N=1,NTOT)
  192 RETURN
  300 IF(NBUG6.NE.0) WRITE(IWRITE,302)
      RETURN
  301 IF(NBUG6.NE.0) WRITE(IWRITE,303)
      RETURN
      END
*
*     ------------------------------------------------------------------
*
*	T R I T S T
*     ------------------------------------------------------------------
*
      DOUBLE PRECISION FUNCTION TRITST(L,M,N)
*
      IMPLICIT REAL *8(A-H,O-Z)
*
*
*      IF  TRITST=1.0   THE TRIANGLE RELATION IS NOT SATISFIED
*      IF  TRITST=0.0   THE TRIANGLE RELATION IS SATISFIED
*
      LMN=IABS(L-M)
      LM=L+M
      IF(N-LMN) 1,2,2
    2 IF(LM-N) 1,3,3
    3 TRITST=0.D0
      RETURN
    1 TRITST=1.D0
      RETURN
      END
